# fish configuration, see: <https://nix-community.github.io/home-manager/options#opt-programs.fish.enable>
{
  config,
  osConfig,
  ...
}: let
  defaults = {
    "config.nu" = builtins.fetchurl {
      url = "https://raw.githubusercontent.com/nushell/nushell/main/crates/nu-utils/src/sample_config/default_config.nu";
      sha256 = "02qqjlhgyv4rcjzk8zvgk9q58fjykvrjkymi84gmnbhxw0j2pcrp";
    };
    "env.nu" = builtins.fetchurl {
      url = "https://raw.githubusercontent.com/nushell/nushell/main/crates/nu-utils/src/sample_config/default_env.nu";
      sha256 = "03r7jinb2b0qgnycddibbspblf6h4136f0d3nn3x5kkir2ij0nhl";
    };
  };
in {
  config.programs.nushell = {
    enable = true;
    configFile.text = (
      builtins.concatStringsSep "\n" [
        "### generated by home-manager: default config.nu"
        (builtins.readFile defaults."config.nu")
        "### generated by home-manager: user config.nu"
        (builtins.readFile ./config.nu)
      ]
    );

    envFile.text = (
      builtins.concatStringsSep "\n" [
        "### generated by home-manager: default env.nu"
        (builtins.readFile defaults."env.nu")
        "### generated by home-manager: user env.nu"
        (builtins.readFile ./env.nu)
      ]
    );

    extraEnv = ''
      ### generated by home-manager: extra env.nu

      $env.PATH = (
          $env.PATH
          | split row (char esep)
          # Add macOS system paths.
          | append '/sbin'
          | append '/usr/sbin'
          | append '/usr/local/bin'
          # Add Nix paths.
          | append ($env.HOME | path join '.nix-profile/bin')
          | append '/etc/profiles/per-user/${osConfig.username}/bin'
          | append '/nix/var/nix/profiles/default/bin'
          | append '/run/current-system/sw/bin'
          # Add homebrew paths.
          | append '/opt/homebrew/bin'
          # Add application paths.
          | append '/Applications/Wireshark.app/Contents/MacOS'
          | uniq
      )

      # sessionVariables aren't respected yet, set it manually.
      # See: <https://github.com/nix-community/home-manager/issues/4620>
      $env.SSH_AUTH_SOCK = '${config.home.sessionVariables.SSH_AUTH_SOCK}'
    '';
  };
}
